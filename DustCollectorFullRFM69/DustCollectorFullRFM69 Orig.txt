
/*
*	Dust Collector Full board code, Copyright Jonathan Mackey 2018
*
*
*	GNU license:
*	This program is free software: you can redistribute it and/or modify
*	it under the terms of the GNU General Public License as published by
*	the Free Software Foundation, either version 3 of the License, or
*	(at your option) any later version.
*
*	This program is distributed in the hope that it will be useful,
*	but WITHOUT ANY WARRANTY; without even the implied warranty of
*	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
*	GNU General Public License for more details.
*
*	You should have received a copy of the GNU General Public License
*	along with this program.  If not, see <http://www.gnu.org/licenses/>.
*
*	Please maintain this license information along with authorship and copyright
*	notices in any redistribution of this code.
*
*
*
*	This very simple sketch triggers an alarm once the voltage across the motor
*	control MOSFET exceeds kTriggerThreshold.  The design assumes the use of a
*	motor control MOSFET with a fairly high on resistance by today's standards.
*	In this case a BSS138 is used.
*
*	This code uses a modified version of Felix Rusu's RFM69 library.
*	Copyright Felix Rusu 2016, http://www.LowPowerLab.com/contact
*
*	The RFM69 library modifications allow it to be used on an ATtiny84 MCU.
*	I shedded some code (about 1K), adjusted the SPI speed, and made some
*	optimizations.
*
*	This code also uses a modified version of Jack Christensen's tinySPI Library.
*	Copyright 2018 Jack Christensen https://github.com/JChristensen/tinySPI
*
*	The tinySPI Library modification allows for 800KHz, 2MHz and 4MHz SPI speeds
*	when used with an 8MHz cpu clock.  The original code allowed for only a
*	single speed of approximately 666KHz with a 8MHz clock.
*/
#include <Arduino.h>
#include <avr/interrupt.h>
#include <RFM69.h>    // https://github.com/LowPowerLab/RFM69
#include <tinySPI.h>

/*
*	IMPORTANT RADIO SETTINGS
*/
#define NODEID            56	// Unique for each node on same network
#define NETWORKID         100	// The same on all nodes that talk to each other
#define GATEWAYID         1
//Frequency of the RFM69 module:
#define FREQUENCY         RF69_433MHZ
//#define FREQUENCY         RF69_868MHZ
//#define FREQUENCY         RF69_915MHZ

/*
*	Uploading a new sketch:
*	The DCF board has a standard ICSP header and a PROG/RUN DPDT switch.  Pin 1
*	of the ICSP header is as marked on the board.  When programming, the switch
*	should be in the PROG position (closest to the ICSP header.)  Any 3v3 ISP
*	can be used.  Do not use a 5 volt ISP, you may damage the RFM69.  The ICSP
*	cable I use is only 5 wire, with 3v3 not connected (not supplied to the
*	board by the ISP.)  If you use a 6 wire cable then obviously disconnect the
*	12V power.
*/

/*
*	If the current threshold setting is not adequate (too much/too little):
*	USE_SERIAL is used to determine the amount of torque needed to trigger an
*	alarm. Connect a 3v3 serial board to the serial connection on the DCF board.
*	From the edge of the board: GND TX RX (you only need TX connected to RX of
*	the serial board + GND)
*	From the Arduino IDE or any other serial terminal, connect to the serial
*	port of the DCF module.  Turn on the module and squeeze the motor shaft till
*	till the desired trigger torque is reached.  Use the value displayed to set
*	the value of kTriggerThreshold below.
*	WARNING: When using USE_SERIAL the motor is not stopped at a threshold value.
*	Do not completely stop the motor shaft, you could damage the MOSFET that
*	controls the motor.  
*/
//#define USE_SERIAL
#ifdef USE_SERIAL
#include <SoftwareSerial.h>
#define SERIAL_BAUD   9600
#define RxPin 2			// PA2
#define TxPin 3			// PA3
SoftwareSerial swSerial(RxPin, TxPin); // RX, TX

#endif

#define HAS_RFM69
#ifdef HAS_RFM69
RFM69 radio;
#endif

#define MotorControlPin	9	// PB1 Turns the motor MOSFET off/on
#define FlasherPin		1	// PA1 Turns the flasher MOSFET off/on
#define MotorVoltagePin 0	// PA0 1/2 voltage across MOSFET. 

static bool	sMotorStopped = false;
#define RING_BUFF_SIZE 5
static uint32_t	sRingBuf[RING_BUFF_SIZE];
static uint8_t	sRingBufIndex = 0;
static const uint32_t kTriggerThreshold = 30;

/********************************* setup **************************************/
void setup(void)
{    
#ifdef USE_SERIAL
	swSerial.begin(9600);
	delay(10);
	swSerial.println("Starting...");
#endif  
	pinMode(FlasherPin, OUTPUT);
	pinMode(MotorControlPin, OUTPUT);
	digitalWrite(MotorControlPin, HIGH);
	digitalWrite(FlasherPin, LOW);

#ifdef HAS_RFM69
	radio.initialize(FREQUENCY,NODEID,NETWORKID);

	radio.sendWithRetry(GATEWAYID, "START", 6);

	radio.sleep();
#endif
	delay(2000);	// Give the motor a chance to start.
}

#ifdef USE_SERIAL
static uint32_t lastAverage;
#endif
/********************************** loop **************************************/
void loop(void)
{
	if (!sMotorStopped)
	{
		sRingBuf[sRingBufIndex] = analogRead(MotorVoltagePin);
		sRingBufIndex++;
		if (sRingBufIndex >= RING_BUFF_SIZE)
		{
			sRingBufIndex = 0;
		}
		uint32_t average = sRingBuf[0];
		for (uint8_t i = 1; i < RING_BUFF_SIZE; i++)
		{
			average += sRingBuf[i];
		}
		average /= RING_BUFF_SIZE;
		#ifndef USE_SERIAL
		if (average > kTriggerThreshold)
		{
			sMotorStopped = true;
			digitalWrite(MotorControlPin, LOW);
			digitalWrite(FlasherPin, HIGH);
		#ifdef HAS_RFM69
			radio.sendWithRetry(GATEWAYID, "DCF", 4);
			radio.sleep();
		#endif
		}
		#else
		if (average != lastAverage)
		{
			lastAverage = average;
			swSerial.println(average);
		}
		#endif
	}
}
